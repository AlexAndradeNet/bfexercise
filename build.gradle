plugins {
    id "java"
    id 'eclipse'
    id 'idea'
    id "org.sonarqube" version "5.0.0.4638"
    id "net.serenity-bdd.serenity-gradle-plugin" version "4.1.10"
}

defaultTasks 'clean', 'test', 'aggregate'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

apply from: "$rootDir/gradle/libraries.gradle"

dependencies {
    implementation libs.aeonbits,
            libs.slf4j.api,
            libs.slf4j.simple

    compileOnly libs.gson

    testImplementation libs.test.serenity.core,
            libs.test.serenity.ensure,
            libs.test.serenity.screenplay,
            libs.test.serenity.screenplayWebdriver,
            libs.test.serenity.cucumber,
            libs.test.junit.engine,
            libs.test.junit.launcher,
            libs.test.junit.suite,
            libs.test.cucumber.java,
            libs.test.cucumber.junit

}

test {
    testLogging.showStandardStreams = true
    systemProperties System.getProperties()
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

test.finalizedBy(aggregate)

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

//gradle.startParameter.continueOnFailure = true

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
